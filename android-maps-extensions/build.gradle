buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

android {
    compileSdkVersion 19
    buildToolsVersion '19'

    defaultConfig {
        minSdkVersion 8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services:4.2.42'
}

group = 'com.androidmapsextensions'
version = '2.1.1-SNAPSHOT'

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives.repositories.mavenDeployer {

    beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
    }

    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
        authentication(userName: sonatypeUserName, password: sonatypePassword)
    }
    snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
        authentication(userName: sonatypeUserName, password: sonatypePassword)
    }

    pom.project {
        name 'Android Maps Extensions'
        description 'Library extending capabilities of Google Maps Android API v2'
        inceptionYear '2013'
        url 'http://AndroidMapsExtensions.com'
        packaging 'aar'

        developers {
            developer {
                name 'Maciej GÃ³rski'
                id 'mg6maciej'
                email 'maciek.gorski@gmail.com'
                url 'http://mg6.pl'
            }
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        scm {
            url 'https://github.com/mg6maciej/android-maps-extensions'
            connection 'scm:git:https://github.com/mg6maciej/android-maps-extensions.git'
            developerConnection 'scm:git:https://github.com/mg6maciej/android-maps-extensions.git'
        }
    }

    pom.whenConfigured {
        pom -> pom.dependencies = []
    }
}

android.libraryVariants.all { variant ->
    variant.processManifest.doFirst {
        File manifestFile = file("${buildDir}/exploded-aar/com.google.android.gms/play-services/4.2.42/AndroidManifest.xml")
        if (manifestFile.exists()) {
            String content = manifestFile.getText('UTF-8')
            content = content.replaceAll(/minSdkVersion="9"/, 'minSdkVersion="8"')
            manifestFile.write(content, 'UTF-8')
        }
    }
}
