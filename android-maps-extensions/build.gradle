apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def isCI = System.env.CI == "true"

ext {
    localBuildToolsVersion = project.hasProperty("AME_BUILD_TOOLS_VERSION") ? AME_BUILD_TOOLS_VERSION : '28.0.3'
    localCompileSdkVersion = project.hasProperty("AME_COMPILE_SDK_VERSION") ? AME_COMPILE_SDK_VERSION : 25
    localMinSdkVersion = project.hasProperty("AME_MIN_SDK_VERSION") ? AME_MIN_SDK_VERSION : 8
    playServicesVersion = project.hasProperty("AME_PLAY_SERVICES_VERSION") ? AME_PLAY_SERVICES_VERSION : '10.2.4'
}

android {
    compileSdkVersion localCompileSdkVersion
    buildToolsVersion localBuildToolsVersion

    defaultConfig {
        minSdkVersion localMinSdkVersion
    }
    dexOptions {
        preDexLibraries = !isCI
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.google.android.gms:play-services-maps:${playServicesVersion}"
}

group = 'com.androidmapsextensions'
version = '2.4.1-SNAPSHOT'

if (!isCI) {

    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    uploadArchives.repositories.mavenDeployer {

        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: sonatypeUserName, password: sonatypePassword)
        }
        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: sonatypeUserName, password: sonatypePassword)
        }

        pom.project {
            name 'Android Maps Extensions'
            description 'Library extending capabilities of Google Maps Android API v2'
            inceptionYear '2013'
            url 'http://AndroidMapsExtensions.com'
            packaging 'aar'

            developers {
                developer {
                    name 'Maciej GÃ³rski'
                    id 'mg6maciej'
                    email 'maciek.gorski@gmail.com'
                    url 'https://github.com/mg6maciej'
                }
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                url 'https://github.com/mg6maciej/android-maps-extensions'
                connection 'scm:git:git@github.com:mg6maciej/android-maps-extensions.git'
                developerConnection 'scm:git:git@github.com:mg6maciej/android-maps-extensions.git'
            }
        }

        pom.whenConfigured {
            pom -> pom.dependencies = []
        }
    }
}
